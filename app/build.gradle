apply plugin: 'com.android.application'
apply plugin: 'walle'
apply plugin: 'channel'
def dbflow_version = "4.2.4"

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }

    //解决aapt2签名打包出现的错误
    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false
    }

    //解决打包过程中出现的错误
    flavorDimensions 'api'

    productFlavors {
        _test {}
        qh360 {}
        baidu {}
        yingyongbao {}
        wandj {}
        xiaomi {}
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == "release") {
                outputFileName = "androidtest_${variant.productFlavors[0].name}_code_${variant.versionCode}_v${variant.versionName}_release.apk"
            } else {
                outputFileName = "androidtest_${variant.productFlavors[0].name}_code_${variant.versionCode}_v${variant.versionName}_debug.apk"
            }
        }
    }

    //签名配置
    signingConfigs {
        release {
            keyAlias 'key'
            keyPassword '11111111'
            storeFile file('keystore')
            storePassword '11111111'
        }
    }

    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        abortOnError false
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }

        release {
            //可调式
            debuggable false
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled false
            //移除无用的resource文件
            shrinkResources false
            //混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名配置
            signingConfig signingConfigs.release
        }
    }

    buildToolsVersion rootProject.ext.android.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            jni.srcDirs = []
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.dependencies["support-v4"]
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["design"]

    //recyclerview
    implementation rootProject.ext.dependencies["recyclerview"]

    //工具类
    implementation 'com.blankj:utilcode:1.17.1'

    //权限
    implementation 'pub.devrel:easypermissions:1.2.0'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    //glide-transformations
    implementation 'jp.wasabeef:glide-transformations:3.3.0'
    //glide Okhttp3
    implementation "com.github.bumptech.glide:okhttp3-integration:4.7.1"

    //recyclerview helper
    implementation "cc.solart:turbo-recyclerview-helper:1.2.1"

    //lifecycle
    implementation rootProject.ext.dependencies["lifecycle"]

    //banner
    implementation 'com.youth.banner:banner:1.4.10'

    //viewanimator
    implementation 'com.github.florent37:viewanimator:1.0.5'

    //FlycoTabLayout
    implementation 'com.flyco.tablayout:FlycoTabLayout_Lib:2.1.2@aar'

    //joor
    implementation group: 'org.jooq', name: 'joor', version: '0.9.6'

    //filedownloader
    implementation 'com.liulishuo.filedownloader:library:1.7.3'

    //dbflow
    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
    // sql-cipher database encyrption (optional)
    implementation "com.github.Raizlabs.DBFlow:dbflow-sqlcipher:${dbflow_version}"

    //stetho
    implementation 'com.facebook.stetho:stetho:1.5.0'

    //OkGo
    implementation 'com.lzy.net:okgo:3.0.4'

    implementation 'com.lzy.net:okserver:2.0.5'

    //Gson
    implementation 'com.google.code.gson:gson:2.6.2'

    //walle
    implementation 'com.meituan.android.walle:library:1.1.6'

    //VasDolly
    api 'com.leon.channel:helper:2.0.1'

    //二维码
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'


}
//美团多渠道打包
walle {
    // 指定渠道包的输出路径
    apkOutputFolder = new File("${project.buildDir}/outputs/channels");
    // 定制渠道包的APK的文件名称
    apkFileNameFormat = '${appName}-${packageName}-${channel}-${buildType}-v${versionName}-${versionCode}-${buildTime}.apk';
    // 渠道配置文件
    channelFile = new File("${project.getProjectDir()}/channel")
}

//腾讯多渠道打包
channel {
    //指定渠道文件
    channelFile = file(project.rootDir.getAbsolutePath() + File.separator + "channel.txt")
    //多渠道包的输出目录，默认为new File(project.buildDir,"channel")
    baseOutputDir = new File(project.rootDir, "tencent_channel")
    //多渠道包的命名规则，默认为：${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}
    apkNameFormat = '${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}'
    //快速模式：生成渠道包时不进行校验（速度可以提升10倍以上，默认为false）
    isFastMode = false
    //buildTime的时间格式，默认格式：yyyyMMdd-HHmmss
    buildTimeDateFormat = 'yyyyMMdd-HH:mm:ss'
    //低内存模式（仅针对V2签名，默认为false）：只把签名块、中央目录和EOCD读取到内存，不把最大头的内容块读取到内存，在手机上合成APK时，可以使用该模式
    lowMemory = false
}
